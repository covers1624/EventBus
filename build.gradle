plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

group 'org.example'
version '0.1.0'

version = "$version." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of ${archivesBaseName}, Version: ${version}"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.covers1624.net' }
}

configurations {
    testCompileOnly.extendsFrom compileOnly
    testImplementation.extendsFrom implementation
}

dependencies {
    api 'org.slf4j:slf4j-api:1.7.32'

    api 'org.ow2.asm:asm:9.2'
    api 'org.ow2.asm:asm-tree:9.2'
    api 'org.ow2.asm:asm-commons:9.2'
    api 'org.ow2.asm:asm-analysis:9.2'
    api 'com.google.guava:guava:30.1.1-jre'
    api 'net.covers1624:Quack:0.4.6.69'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.jetbrains:annotations:24.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        publication(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            pom {
                name = archivesBaseName
                description = archivesBaseName
                url = "https://github.com/covers1624/${archivesBaseName}"
                scm {
                    url = "https://github.com/covers1624/${archivesBaseName}"
                    connection = "scm:git:git://github.com/covers1624/${archivesBaseName}.git"
                    connection = "scm:git:git@github.com:covers1624/${archivesBaseName}.git"
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/covers1624/${archivesBaseName}/issues"
                }
                licenses {
                    license {
                        name = 'MIT'
                        url = "https://raw.githubusercontent.com/covers1624/${archivesBaseName}/master/LICENSE.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
            }
        }
    }
}
